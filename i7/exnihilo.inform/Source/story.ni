"Ex Nihilo" by Juhana Leinonen

[
Copyright (c) 2012-2013 Juhana Leinonen.

Permission is hereby granted, free of charge, to any person obtaining a copy of 
this software and associated documentation files (the "Software"), to deal in 
the Software without restriction, including without limitation the rights to 
use, copy, modify, merge, publish, distribute, sublicense, and/or sell copies of
the Software, and to permit persons to whom the Software is furnished to do so, 
subject to the following conditions:

The above copyright notice and this permission notice shall be included in all 
copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR 
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS
FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR
COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER 
IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN 
CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
]

Book Extensions

Include Basic Screen Effects by Emily Short.


Book Nodes

Table of nodes
previous (indexed text)	keyword (text)	content	chosen (truth state)
""	"light"	"I watched the universe define itself around me. Particles, atoms and molecules engaged in an intricate dance formed more and more complex structures.[paragraph break]For eons I was ALONE."
""	"darkness"	"I watched the universe define itself around me. Particles, atoms and molecules engaged in an intricate dance formed more and more complex structures.[paragraph break]For eons I was ALONE."
"light"	"alone"	"RESTLESS - LONELY - HOPEFUL - DEPRESSED"
"alone"	"restless"	"I was restless.[paragraph break]My wanderlust took me everywhere in the newborn universe. I saw worlds that were desolate and barren, beautiful and lush, strange and dangerous.[paragraph break]Then I found a world that was inhabited by curious creatures who were explorers by heart. I knew that I would WANDER with them and GUIDE them in their travels."
"restless"	"wander"	"I joined the creatures in their exploration of their world. We discovered new lands and gained new horizons. We strived DEEPER and FARTHER together." [5]
"restless"	"guide"	"The creatures explored their world, discovered new lands and gained new horizons. They strived DEEPER and FARTHER with my guidance."
"wander"	"deeper"	"Their society progressed and wisened. They knew their world inside out, and they knew themselves. Their world was a beacon of light in darkness."
"guide"	"deeper"	"Their society progressed. They came to me for wisdom, and I provided it to them. We created a world of truth and beauty."
"wander"	"farther"	"Soon their world was mapped from end to end and they turned their attention beyond. They built machines taking them to other worlds where their lust for discovery could be kindled and satiated again and again."
"guide"	"farther"	"Soon the world was mapped from end to end and we turned our attention beyond. I watched them build machines that took them to other worlds, unfamiliar only to them." [10]
"alone"	"lonely"	"Being lonely was what defined me.[paragraph break]I sought the company of my kind, even though I knew there was no-one else. I was craving for SOLITUDE and desperate for CONTACT."
"lonely"	"solitude"	"I took comfort in the emptiness of space. Time passed and I didn't wake from my stupor until a world was formed in the vicinity. Soon the world was inhabited by creatures unlike me, but still: they were something instead of nothing.[paragraph break]My instinct was to EMBRACE them or SHUN them."
"lonely"	"contact"	"I travelled the universe to find an escape from the loneliness. Ages passed and I found a world that was inhabited by creatures much unlike me. Still, they were there for me.[paragraph break]Now that I had found them, I wanted to both EMBRACE and SHUN them."
"solitude"	"embrace"	"They were simple beings but I enjoyed their company as much as I could. I watched them grow, fight, learn, stumble and rise. I grew to like them."
"contact"	"embrace"	"My travels had not been in vain, even though the creatures were so small and simple. I took to them and they accepted me, and their world was to become my home." [15]
"solitude"	"shun"	"I had existed alone through the passage of time for as long as time had existed. The creatures were of no comfort to me. They lived for but a blink of an eye, lives without meaning or understanding. I left them alone."
"contact"	"shun"	"I was delighted but kept my distance. They were curious beings but they did not satisfy my need for a meaningful contact."
"alone"	"hopeful"	"Yet I remained hopeful.[paragraph break]After an unmeasurable span of time I found life. The strange creatures were alone and scared, looking for someone to SUPPORT and COMFORT them."
"hopeful"	"support"	"The creatures needed something to lean on, and I provided it. Their society bloomed and they produced ART and KNOWLEDGE."
"hopeful"	"comfort"	"I comforted them so that they could overcome their fears. Their society bloomed and they produced ART and KNOWLEDGE." [20]
"support"	"art"	"We created beautiful art. Divine sights and sounds would fill their hearts with pride and content."
"comfort"	"art"	"They created art. Beautiful sights and sounds would help them carry their burdens."
"support"	"knowledge"	"We strived for knowledge. Science and philosophy flourished and the creatures made giant leaps as a culture."
"comfort"	"knowledge"	"They strived for knowledge. Science and philosophy advanced the creatures as a culture."
"alone"	"depressed"	"I reclused to a barren and dead world. Time passed, but I was not left alone. Life appeared and grew into strange creatures. I was tempted to JOIN them or OBSERVE them." [25]
"depressed"	"join"	"The creatures were curious beings and I wanted to learn more. When we were inspired we would CREATE; when they were tired they would SLEEP, and I would watch over them."
"depressed"	"observe"	"I chose to observe the creatures from afar as they stumbled on. When they were inspired they would CREATE; when they were tired they would SLEEP. I was intrigued."
"join"	"create"	"We built great and marvelous things. Towers that reached the skies and machines that would take us outside our world. We achieved things beyond our dreams."
"observe"	"create"	"They built great things. Time would destroy what they made, just so that they could build them again and better."
"join"	"sleep"	"The creatures reached the potential of their civilization. We stopped, and we were content." [30]
"observe"	"sleep"	"The creatures reached the height of their civilization--and stopped."
"darkness"	"alone"	"ANXIOUS - ANGRY - ENTHUSIASTIC - FORSAKEN"
"alone"	"anxious"	"I was anxious to find a purpose.[paragraph break]I scoured the universe but found no solace.[paragraph break]Until I stumbled upon a world home to a curious kind of creatures who were ready to accept CONTROL and INFLUENCE."
"anxious"	"control"	"They took me as their god. I led them into WAR and their world succumbed into CHAOS."
"anxious"	"influence"	"They were like clay in my grip, fulfilling my every wish while believing they did it in their own free will. I guided them to wage WAR and spread CHAOS around them." [35]
"control"	"war"	"I was their commander in chief, waging war with everyone and no-one in particular. Borders changed, property and land changed owners and death was ever-present."
"influence"	"war"	"It did not take much to usher them into war. They were proud, greedy, stubborn and, ultimately, dead."
"control"	"chaos"	"There was no progress, no growth; everything they built would be destroyed. While they were busy surviving they had no time to question my authority."
"influence"	"chaos"	"Their society was in constant turmoil, falling down before it could be rebuilt. I kept them preoccupied with petty quarrels and self-interest."
"alone"	"angry"	"Angry was all I could be.[paragraph break]My rage required a target. I had to feed it with DEATH and MISERY." [40]
"angry"	"death"	"I found life. Where there is life, there is death. These miserable creatures lived their lives full of SUFFERING and were forgotten without MERCY in the vastness of time."
"angry"	"misery"	"There was nothing for me but misery, and I was not about to bear it alone.[paragraph break]I found creatures that were in their own way alone but oblivious to their condition. SUFFERING was inevitable; MERCY was a privilege."
"death"	"suffering"	"Their miserable lives could not be cured with anything short of death, and inescapable death would soon take them."
"misery"	"suffering"	"Misery leads to suffering, and there is no escape from the pain that surrounds us and defines us."
"death"	"mercy"	"They were hopeless creatures with nothing to look forward to other than death. Death would be merciful, and it would come for them." [45]
"misery"	"mercy"	"They were suffering creatures with nothing to look forward to other than agony. I took pity on them and treated them kindly."
"alone"	"enthusiastic"	"I was enthusiastic about the future.[paragraph break]After a while life appeared, and I encountered a curious breed of creatures. I would WATCH them and LEARN from them."
"enthusiastic"	"watch"	"I watched as the creatures went on with their lives, but I did not UNDERSTAND them, and I did not see MEANING in their ways."
"enthusiastic"	"learn"	"The creatures did, thought and felt things I did not comprehend. I craved to UNDERSTAND, to find a MEANING in their ways."
"watch"	"understand"	"I did understand: their small minds were unable to comprehend the uselessness and futility of their menial tinkering in a speck of dust meaningless to the universe. Their ignorance mocked me, enraged me." [50]
"learn"	"understand"	"Or the surface their existence served no purpose, and further study would not change that fact. They went on without knowing, or caring, that their lives were for naught."
"watch"	"meaning"	"On the surface their existence served no purpose. They went on without knowing, or caring, that their lives had no meaning. I could not tolerate them anymore, and abandoned them."
"learn"	"meaning"	"Their lives were meaningless, but only on the surface. Millennia later I had merged with them, become one of them. Like them, I no longer cared for finding a meaning."
"alone"	"forsaken"	"I encountered a breed of miserable creatures. I would become their GOD and they would learn the meaning of FEAR."
"forsaken"	"god"	"They worshipped me, and I ruled them with iron grip. They too would feel my ISOLATION and DESPAIR." [55]
"forsaken"	"fear"	"There is no hope; there is no escape. There is only ISOLATION and DESPAIR."
"god"	"isolation"	"The creatures would look to the skies and dream. Their god would look at them and strike them down, for they shall never leave."
"fear"	"isolation"	"The creatures would look to the skies and fear the unknown. They would stay in their world and diminish, as if it were their own will."
"god"	"despair"	"They would have no future for I would not allow it for them."
"fear"	"despair"	"They would have no future for they would not allow it for themselves." [60]
"light-presence"	"presence"	"CURIOSITY - NERVOUSNESS - UNITY"
"darkness-presence"	"presence"	"INTEREST - DREAD - AFFINITY"
"presence"	"curiosity"	""
"presence"	"nervousness"	""
"presence"	"unity"	"" [65]
"presence"	"interest"	""
"presence"	"dread"	""
"presence"	"affinity"	""
"word"	"fate"	""
	
Book Rules

There is a room.

When play begins (this is the explanation rule):
	say "You are about to play the text-only version of [italic type]Ex Nihilo[roman type]. If at all possible, you are encouraged to play the full online version at http://nitku.net/if/exnihilo for a more complete experience. Apart from the multimedia features, this version is missing a 'magic ingredient' which is impossible to replicate offline.[paragraph break]The usual parser is replaced by a keyword interface. Play by typing capitalized keywords or as otherwise instructed.";
	pause the game;
	say paragraph break.

When play begins (this is the intro rule):
	say "In the beginning there was nothing.[paragraph break]From nothing LIGHT and DARKNESS were born."
	
When play begins (this is the fill table rows rule):
	repeat through the table of nodes:
		now the chosen entry is false.
	
	
The current node is indexed text that varies. The current node is "".
The turn counter is a number that varies. The turn counter is 0.

To decide whether (word - indexed text) has been chosen:
	repeat through the table of nodes:
		if the word is the keyword entry and chosen entry is true:
			decide yes;
	decide no.

To decide whether (word - indexed text) has not been chosen:
	if word has been chosen, decide no;
	decide yes.

To decide which text is the alignment:
	if "light" has been chosen:
		decide on "light";
	if "darkness" has been chosen:
		decide on "darkness";
	decide on "neutral".
	
After reading a command:
	let command be indexed text;
	let command be the player's command;
	if the turn counter is 7: ["What shall I say?"]
		say "[italic type]'[command]'[paragraph break][what the other says][roman type][paragraph break]Our encounter is the most significant event since the Beginning, and it will determine the FATE of this world.";
		now the current node is "word";
		increment the turn counter;
		reject the player's command;
	if the turn counter is 8: [final word]
		end the story finally saying "[final word]";
		follow the print player's obituary rule;
		stop game abruptly;
	repeat through table of nodes:
		if previous entry is the current node and  the command is the keyword entry:
			increment the turn counter;
			now the current node is "[command]";
			now the chosen entry is true;
			if the turn counter is 7: [second keyword cloud]
				say "There's another being, another like me: it is here, and we have met each other.[paragraph break]What shall I say?";
				reject the player's command;
			say the content entry;
			say line break;
			if the turn counter is 5: [presence]
				say "[line break]Then I sensed a PRESENCE.[line break]";
				now the current node is "[alignment]-presence";
			reject the player's command;
	say "'[command]' is not a valid keyword at the moment.";
	reject the player's command.
	
To say what the other says:
	[The quotes must be here to prevent line break issues]
	if alignment is "light": 
		say "[one of]'Who are you?'[or]'I am Death.'[or]'You shall suffer.'[or]'There is no escape.'[or]'Why?'[or]'Join me.'[or]'Finally.'[stopping]";
	otherwise:
		say "[one of]'Who are you?'[or]'I am Life.'[or]'We shall become one.'[or]'What is our purpose?'[or]'Is this how it ends?'[stopping]";

[We can use the fact that the table where the choices are is quite symmetrical.]
To randomize other player's choices:
	let base row be a number;
	if alignment is "darkness":
		now base row is 4;
	otherwise:
		now base row is 33;
	increase the base row by ( a random number between 0 and 3 ) * 7;
	mark base row chosen;
	mark base row + a random number between 1 and 2 chosen;
	mark base row + a random number between 4 and 5 chosen.
	
To say final word:
	[this is *really* slow, by the way.]
	randomize other player's choices;
	repeat through the table of final words:
		let ok be true;
		repeat with choice running through requirement entry:
			if choice has not been chosen:
				now ok is false;
				break;
		if ok is true:
			say word entry;
			break.
	
To mark (N - a number) chosen:
	choose row N in the table of nodes;
	now chosen entry is true.

Table of final words
word	requirement
"Dominance"	{ "farther", "war" } 
"Resistance"	{ "deeper", "war" } 
"Regression"	{ "restless", "chaos" } 
"Diminishment"	{ "farther", "angry" }
"Legacy"	{ "deeper", "angry" }
"Enlightenment"	{ "deeper", "learn", "meaning" }
"Discovery"	{ "farther", "learn", "meaning" }
"Independence"	{ "restless", "watch", "meaning" }
"Ennui"	{ "restless", "meaning" }
"Legacy"	{ "deeper", "watch", "understand" }
"Tragedy"	{ "father", "watch", "understand" }
"Nihilism"	{ "deeper", "understand" }
"Emptiness"	{ "farther", "understand" }
"Delusion"	{ "farther", "god", "isolation" }
"Extinction"	{ "restless", "isolation" }
"Extinguishment"	{ "restless", "despair" }
"Struggle"	{ "embrace", "anxious" }
"Submission"	{ "shun", "chaos" }
"Thralldom"	{ "shun", "war" }
"Irrelevance"	{ "lonely", "death" }
"Harmony"	{ "embrace", "misery", "mercy" } 
"Dictatorship"	{ "shun", "misery", "mercy" } 
"Agony"	{ "lonely", "misery", "suffering" } 
"Dereliction"	{ "solitude", "shun", "watch", "meaning" }
"Distancing"	{ "contact", "shun", "meaning" }
"Indifference"	{ "shun", "enthusiastic" }
"Zeal"	{ "embrace", "watch", "understand" }
"Warmth"	{ "embrace", "enthusiastic" }
"Benevolence"	{ "embrace", "isolation" }
"Resistance"	{ "embrace", "despair" }
"Confinement"	{ "shun", "isolation" }
"Apathy"	{ "shun", "despair" }
"Optimism"	{ "art", "war" }
"Anarchy"	{ "art", "chaos" }
"Destruction"	{ "knowledge", "anxious" }
"Melancholy"	{ "art", "death" }
"Sacrifice"	{ "art", "suffering" }
"Bitterness"	{ "knowledge", "death" }
"Progress"	{ "knowledge", "misery" }
"Divinity"	{ "art", "angry" }
"Naivete"	{ "hopeful", "learn", "understand" }
"Martyrdom"	{ "art", "watch", "understand" }
"Beauty"	{ "art", "meaning" }
"Misbelief"	{ "knowledge", "watch", "understand" }
"Vanity"	{ "knowledge", "meaning" }
"Futility"	{ "art", "forsaken" }
"Complacency"	{ "knowledge", "isolation" }
"Pestilence"	{ "knowledge", "despair" } 
"Ruination"	{ "create", "war" }
"Downfall"	{ "create", "chaos" }
"Aggression"	{ "sleep", "war" }
"Lethargy"	{ "sleep", "chaos" }
"Misanthropy"	{ "create", "suffering" }
"Spirituality"	{ "create", "mercy" }
"Passivity"	{ "sleep", "suffering" }
"Detachment"	{ "sleep", "mercy" }
"Loss"	{ "create", "watch", "understand" }
"Pretentiousness"	{ "create", "learn", "understand" }
"Grandioseness"	{ "create", "meaning" }
"Conformance"	{ "sleep", "understand" }
"Negligence"	{ "sleep", "meaning" }
"Hesitation"	{ "create", "isolation" }
"Resignation"	{ "create", "despair" }
"Withdrawal"	{ "sleep", "isolation" }
"Disinterest"	{ "sleep", "despair" }
"Indecisiveness"	{} [if this is chosen there's a bug]



Book Setup

The display banner rule is not listed in the startup rulebook.
The initial room description rule is not listed in the startup rulebook.

Use MAX_STATIC_DATA of 400000.

Use no scoring.
Use undo prevention.

